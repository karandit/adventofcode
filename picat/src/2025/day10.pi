import util.
import mip.

trimEnds(Ls) = Ls[2..Ls.len-1].

parse(S) = (Light, Wires, Joltage) =>
    Secs = S.split(" "),
    Light = Secs[1].trimEnds.map(parseLight).to_array,
    Wires = Secs.trimEnds.map(parseWire),
    Joltage = Secs[Secs.len].trimEnds.split(",").map(to_int).to_array.

parseWire(S) = S.trimEnds.split(",").map(to_int).

parseLight('.')=0.
parseLight('#')=1.

toggle(Ls, Wr) = LsNew =>
    LsNew = copy_term(Ls),
    foreach(N in Wr)
        N1 = N+1,
        LsNew[N1] := 1-LsNew[N1]
    end.

part1((Lights,Wires,_)) = part1_aux(Lights, {0: _ in 1..Lights.len}, Wires, 0).

part1_aux(Target, Target, _,           Pushes) = Pushes.
part1_aux(_,      _,      [],           _    ) = 100000.
part1_aux(Target, Lights, [Wire|Rest], Pushes) = min(Pushes0, Pushes1) =>
    Pushes0 = part1_aux(Target, Lights,               Rest, Pushes),
    Pushes1 = part1_aux(Target, toggle(Lights, Wire), Rest, Pushes+1).

part2((_, Wires, Joltage)) = Pushes.sum =>
    Buttons = new_array(Wires.len, Joltage.len),
    bind_vars(Buttons, 0),
    foreach (I in 1..Wires.len)
        foreach(Button in Wires[I])
            Buttons[I, Button+1] :=1,
        end
    end,
    ButTrans = Buttons.transpose,
    ButIdxArrs = {{I: I in 1..Row.len, Row[I] == 1 }  :Row in ButTrans  },

    Pushes = new_array(Wires.len),
    foreach(I in 1..Pushes.len)
        Limit = min([Joltage[Btn + 1]: Btn in Wires[I]]),
        Pushes[I] :: 0..Limit,
    end,

    foreach(I in 1..ButIdxArrs.len)
        ButIdxArr = ButIdxArrs[I],
        Joltage[I] #= sum([Pushes[Idx]: Idx in ButIdxArr])
    end,
    Sum #= sum([I: I in Pushes]),
    solve([glpk,$min(Sum)], Pushes).

main([File]) =>
    Ms = File.read_file_lines.map(parse),
    Part1 = [part1(M): M in Ms].sum,
    printf("Part1: %w; ", Part1),
    Part2 = [part2(M): M in Ms].sum,
    printf("Part2: %w%n", Part2).
