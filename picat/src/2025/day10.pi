import util.
import mip.

trimEnds(Ls) = Ls[2..Ls.len-1].

parse(S) = (Light, Wires, Joltage) =>
    Secs = S.split(" "),
    Light = Secs[1].trimEnds.map(parseLight).to_array,
    Wires = Secs.trimEnds.map(parseWire),
    Joltage = Secs[Secs.len].trimEnds.split(",").map(to_int).to_array.

parseWire(S) = S.trimEnds.split(",").map(to_int).

parseLight('.')=0.
parseLight('#')=1.

toggle(Ls, Wr) = LsNew =>
    LsNew = copy_term(Ls),
    foreach(N in Wr)
        N1 = N+1,
        LsNew[N1] := 1-LsNew[N1]
    end.

determine1((Light,Wires,_)) = Sol =>
    L0 = new_array(Light.len),
    bind_vars(L0, 0),

    between(1, 1000, Sol),
    foreach (_ in 1..Sol)
        member(Wr, Wires),
        L0 := toggle(L0, Wr), 
    end,
    foreach(I in 1..Light.len) L0[I] #= Light[I] end,
    solve([$min(Sol)], Sol).

determine2(Wires, ButIdxArrs,Joltage) = Pushes =>
    Pushes = new_array(Wires.len),
    foreach(I in 1..Pushes.len)
        Limit = min([Joltage[Btn + 1]: Btn in Wires[I]]),
        Pushes[I] :: 0..Limit,
    end,

    foreach(I in 1..ButIdxArrs.len)
        ButIdxArr = ButIdxArrs[I],
        Joltage[I] #= sum([Pushes[Idx]: Idx in ButIdxArr])
    end,
    Sum #= sum([I: I in Pushes]),
    solve([glpk,$min(Sum)], Pushes).

main([File]) =>
    Ms = File.read_file_lines.map(parse),
    Part1 = [determine1(M): M in Ms].sum,
    printf("Part1: %w; ", Part1),
    Part2 = 0,
    foreach (M in Ms)
        (_, Wires, Joltage) = M,
        Buttons = new_array(Wires.len, Joltage.len),
        bind_vars(Buttons, 0),
        foreach (I in 1..Wires.len)
            foreach(Button in Wires[I])
                Buttons[I, Button+1] :=1,
            end
        end,
        ButTrans = Buttons.transpose,
        Flags = {{I: I in 1..Row.len, Row[I] == 1 }  :Row in ButTrans  },

        Sol = determine2(Wires, Flags, Joltage),
        Part2 := Part2 + Sol.sum,
    end,
    printf("Part2: %w%n", Part2).
